buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.1.1'
        //dockerJava 'com.github.docker-java:docker-java:2.1.1'
        classpath 'com.bmuschko:gradle-docker-plugin:2.5'

    }
}


apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.bmuschko.docker-remote-api'


import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

version = '3.1.0'
def dockerOutput = new File(buildDir,'docker')
if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}
docker{
    url = 'https://192.168.99.101:2376'
    certPath = new File(System.properties['user.home'], '.docker/machine/machines/gradleBuild')

}
repositories {
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile "io.vertx:vertx-core:$version"
    compile "io.vertx:vertx-web:$version"
    compile "io.vertx:vertx-hazelcast:$version"
    compile 'com.cyngn.vertx:vertx-kafka:0.3.0'
    compile "io.vertx:vertx-redis-client:$version"
    compile "org.slf4j:slf4j-log4j12:1.7.12"
	compile "org.slf4j:slf4j-api:1.7.12"
	compile "log4j:log4j:1.2.17"
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Launcher'
        attributes 'Main-Verticle': 'com.tikal.angelsense.gpsservice.GpsServiceVerticle'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task zipAll(type: Zip) {
    from '.'
	baseName = 'as-gps-service'
    include 'conf/**'
    include '*.sh'
    include 'build/libs/*.jar'
}

task buildImage(type: DockerBuildImage) {
    dependsOn zipAll
    quiet = false
    inputDir = projectDir
    tag = "$registerProdBaseName/$dockerBranch/$project.name:$dockerTag"
}
build.dependsOn buildImage
task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}
